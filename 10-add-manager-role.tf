data "aws_caller_identity" "current" {}

resource "aws_iam_role" "eks_admin" {
  name               = "${var.env}-${var.eks_name}-eks-admin"
  description        = "TF generated"
  assume_role_policy = <<POLICY
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Action": "sts:AssumeRole",
      "Principal": {
        "AWS": "arn:aws:iam::${data.aws_caller_identity.current.account_id}:root"
      }
    }
  ]
}
POLICY
}

resource "aws_iam_policy" "eks_admin" {
  name        = "AmazonEKSAdminPolicy"
  description = "TF generated"
  policy      = file("${path.module}/iam/AmazonEKSAdminPolicy.json")
}

resource "aws_iam_role_policy_attachment" "eks_admin" {
  role       = aws_iam_role.eks_admin.name
  policy_arn = aws_iam_policy.eks_admin.arn
}

resource "aws_iam_user" "manager" {
  name = "manager"
}

resource "aws_iam_policy" "eks_assume_admin" {
  name        = "AmazonEKSAssumeAdminPolicy"
  description = "TF generated"
  policy      = <<POLICY
{
    "Version": "2012-10-17",
    "Statement": [
        {
            "Effect": "Allow",
            "Action": [
                "sts:AssumeRole"
            ],
            "Resource": "${aws_iam_role.eks_admin.arn}"
        }
    ]
}
POLICY
}

resource "aws_iam_user_policy_attachment" "manager" {
  user       = aws_iam_user.manager.name
  policy_arn = aws_iam_policy.eks_assume_admin.arn
}

resource "kubectl_manifest" "admin-cluster-role-binding" {
  yaml_body = file("${path.module}/kubectl_manifest/admin-cluster-role-binding.yaml")
}

# Best practice: use IAM roles due to temporary credentials
resource "aws_eks_access_entry" "manager" {
  cluster_name      = aws_eks_cluster.eks.name
  principal_arn     = aws_iam_role.eks_admin.arn
  kubernetes_groups = ["admin"]
  depends_on        = [kubectl_manifest.admin-cluster-role-binding]
}
